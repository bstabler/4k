 ~$>start
 >>> June 2008 <<<
 Uses minimum transit time by any of 5 transit modes
 ********************************************************************************
 * Macro File Name: VAEMP.MAC                                                   *
 * Original Date: 05/01/2003                                                    *
 * Revised Date:                                                                *
 * Purpose: Prepares "employment circles" for the vehicile availability         *
 *          model -- total employment within 10-minute walk, 30-minute transit  *
 *          ride and 6-mile auto trip. Also calculates government/education and *
 *          university (fte) "employment circles" for the parking cost and      *
 *          time-of-day models -- employment within a 15-minute auto trip.      *
 * Inputs:                                                                      *
 * Temporary:                                                                   *
 * Outputs:                                                                     *
 * Notes:                                                                       *
 * Report File: VAEMP.RPT                                                       *
 ********************************************************************************
 ~:start
 ~/
 ~?!i&32768
 ~o|295
 batchin=?
 reports=VAEMP.RPT
 ~/
 ~/ Move vectors from mf"hhemp0" to md"emptot" and md"eiN"
 ~/ Also extract mo"supret" and mo"supfir" for NW employment
 3.21
 ~+|1|y|mo95|n|mf"hhemp0"|||y|*|109|||2
 ~+|1|y|mo91|n|mf"hhemp0"|||y|*|111|||2
 ~+|1|y|mo92|n|mf"hhemp0"|||y|*|112|||2
 ~+|1|y|mo93|n|mf"hhemp0"|||y|*|113|||2
 ~+|1|y|mo94|n|mf"hhemp0"|||y|*|114|||2
 ~+|1|y|mo"supret"|n|mf"hhemp0"|||y|*|116|||2
 ~+|1|y|mo"supfir"|n|mf"hhemp0"|||y|*|117|||2
 q
 3.12
 ~+|4|3|mo95|md"emptot"|n
 ~+|4|3|mo91|md"ei1"|n
 ~+|4|3|mo92|md"ei2"|n
 ~+|4|3|mo93|md"ei3"|n
 ~+|4|3|mo94|md"ei4"|n
 q
 ~/ CALCULATE EMPLOYEES WITHIN 10 MINUTES BY WALKING
 3.21
 ~+|1|y|mo"empw10"|n|0|||n|2
 ~+|1|y|mo"empw10"|n|md"emptot"*
 (p==q)*((502.655/mo"tazacr").min.1)
 ~+|||y|1,938||1,938||+|2
 ~+|1|y|mo91|n
 md"emptot"*(p!=q)*(1.min.(0.max.(-0.75*(mf"au1dis"/.5)+1.5)))
 ~+||mf"au1dis"|0,0.5,include|y|1,938||1,938||+|2
 ~+|1|y|mo"empw10"|n|mo"empw10"+mo91|||y|1,938||2
 q
 ~/ CALCULATE EMPLOYEES WITHIN 30 MINUTES BY TRANSIT
 3.21
 ~+|1|y|mf91|n|9999|||n|2
 ~+|1|y|mf91|n|mf"aftrtw".max.0||mf"aftrtw"|0.001,9999,in|n|2
 ~+|1|y|mf92|n|9999|||n|2
 ~+|1|y|mf92|n|mf"actrtw".max.0||mf"actrtw"|0.001,9999,in|n|2
 ~+|1|y|mf91|n|mf91.min.mf92||mf92|0.001,9999,in|n|2
 ~+|1|y|mf92|n|9999|||n|2
 ~+|1|y|mf92|n|mf"altrtw".max.0||mf"altrtw"|0.001,9999,in|n|2
 ~+|1|y|mf91|n|mf91.min.mf92||mf92|0.001,9999,in|n|2
 ~+|1|y|mf92|n|9999|||n|2
 ~+|1|y|mf92|n|mf"aptrtw".max.0||mf"aptrtw"|0.001,9999,in|n|2
 ~+|1|y|mf91|n|mf91.min.mf92||mf92|0.001,9999,in|n|2
 ~+|1|y|mf92|n|9999|||n|2
 ~+|1|y|mf92|n|mf"abtrtw".max.0||mf"abtrtw"|0.001,9999,in|n|2
 ~+|1|y|mf91|n|mf91.min.mf92||mf92|0.001,9999,in|n|2
 ~+|1|y|mo"empt30"|n|0|||n|2
 ~+|1|y|mo"empt30"|n|md"emptot"||mf91|0.001,30,include|n|+|2
 q
 ~/ CALCULATE EMPLOYEES WITHIN 6 MILES BY AUTO
 3.21
 ~+|1|y|mo"empa6"|n|0|||n|2
 ~+|1|y|mo"empa6"|n|md"emptot"||mf"au1dis"|0,6,include|n|+|2
 q
 ~/ OUTPUT
 reports=VAEMP.RPT
 3.14
 ~+|1|3|md"emptot"|mo"empw10"|mo"empt30"|mo"empa6"|||n|2
 q
 ~/
 ~/ This macro also prepares the EDU/GOV and UNIV_FTE Employment
 ~/ circles for Parking cost and TOD models.
 ~/ Total employment within 15min by auto.
 ~/
 ~/ Calculate Gov/Ed and FTE Univ for TOD model
 ~/
 reports=VAEMP.RPT
 3.21
 ~+|1|y|mo95|n|mf"hhemp0"|||y|*|115|||2
 ~+|1|y|mo96|n|mf"hhemp0"|||y|*|110|||2
 q
 3.12
 ~+|4|3|mo95|md"govedu"|n
 ~+|4|3|mo96|md96|n
 q
 ~/
 ~/ CALCULATE EDU/GOV EMPLOYEES WITHIN 15 MINUTES BY AUTO
 3.21
 ~+|1|y|mo"edua15"|n|0|||n|2
 ~+|1|y|mo"edua15"|n|md"govedu"||mf"au1cos"|0,15,include|n|+|2
 q
 ~/ CALCULATE UNIV_FTE EMPLOYEES WITHIN 15 MINUTES BY AUTO
 3.21
 ~+|1|y|mo"unva15"|n|0|||n|2
 ~+|1|y|mo"unva15"|n|md96||mf"au1cos"|0,15,include|n|+|2
 q
 ~/ CALCULATE TOTAL EDU/GOV & UNIV_FTE EMPLOYEES WITHIN 15 MINUTES BY AUTO
 3.21
 ~+|1|y|mo"eduunv"|n|mo"edua15"+mo"unva15"|||n|2
 q
 q
